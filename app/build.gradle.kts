import com.android.build.api.dsl.ApplicationDefaultConfig
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.google.service)
    alias(libs.plugins.crashanlytics)
}

kotlin {
    compilerOptions {
        // Target JVM bytecode version (was "11" string, now typed enum)
        jvmTarget = JvmTarget.JVM_17

        // Set Kotlin language and API versions to 2.3
        languageVersion = KotlinVersion.KOTLIN_2_3
        apiVersion = KotlinVersion.KOTLIN_2_3

        // Add experimental/advanced compiler flags
        freeCompilerArgs.addAll(
            "-Xopt-in=kotlin.RequiresOptIn", // Opt-in to @RequiresOptIn APIs
            "-Xwhen-guards",                 // Enable experimental when-guards
            "-Xopt-in=androidx.compose.foundation.ExperimentalFoundationApi", // Compose foundation experimental
            "-Xopt-in=com.zs.compose.theme.ExperimentalThemeApi",             // Custom theme experimental
            "-Xnon-local-break-continue",    // Allow non-local break/continue
            "-Xcontext-sensitive-resolution",// Context-sensitive overload resolution
            "-Xcontext-parameters"           // Enable context parameters (experimental)
        )
    }
}

// The secrets that needs to be added to BuildConfig at runtime.
private val secrets = arrayOf("ADS_APP_ID", "PLAY_CONSOLE_APP_RSA_KEY")

/**
 * Adds a string BuildConfig field to the project.
 */
private fun ApplicationDefaultConfig.buildConfigField(name: String, value: String) =
    buildConfigField("String", name, "\"" + value + "\"")

android {
    buildFeatures { compose = true; buildConfig = true }
    packaging { resources { excludes += "/META-INF/{AL2.0,LGPL2.1}" } }
    namespace = "com.zs.audiofy"
    compileSdk = 36

    // Config. the compose compiler
    composeCompiler {
        // enableStrongSkippingMode = false
        // TODO - I guess disable these in release builds.reportsDestination =
        //     layout.buildDirectory.dir("compose_compiler")
        // metricsDestination = layout.buildDirectory.dir("compose_compiler")
        // stabilityConfigurationFiles = listOf(
        //     rootProject.layout.projectDirectory.file("stability_config.conf")
        // )
    }
    //
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId = "com.googol.android.apps.oneplayer"
        minSdk = 28
        targetSdk = 36
        versionCode = 19
        versionName = "1.5.3-beta"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables { useSupportLibrary = true }
        // Load secrets into BuildConfig
        // These are passed through env of github.
        for (secret in secrets) {
            buildConfigField(secret, System.getenv(secret) ?: "")
        }
    }

    buildTypes {
        // Configuration for the release build type.
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            // signingConfig = signingConfigs.getByName("debug") // 👈 use debug keys here
        }

        // Configuration for the debug build type.
        // Appends ".debug" to the application ID. This allows installing debug and release versions on the same device.
        // Defines a string resource specifically for the debug build.
        debug {
            applicationIdSuffix = ".debug"
            resValue("string", "launcher_label", "Debug")
            versionNameSuffix = "-debug"
        }
    }
    dynamicFeatures += setOf(":feature:telemetry", ":feature:codex")
    composeCompiler {
        // enableStrongSkippingMode = false
        // TODO - I guess disable these in release builds.
        // reportsDestination = layout.buildDirectory.dir("compose_compiler")
        // metricsDestination = layout.buildDirectory.dir("compose_compiler")
        stabilityConfigurationFiles = listOf(
            rootProject.layout.projectDirectory.file("stability_config.conf")
        )
    }
}
// Declare app dependencies
dependencies {
    implementation(libs.androidx.koin)
    implementation(libs.toolkit.preferences)
    implementation(libs.androidx.startup.runtime)
    implementation(libs.androidx.core.splashscreen)
    implementation(libs.accompanist.permissions)
    implementation(libs.androidx.ui.text.google.fonts)
    implementation(libs.chrisbanes.haze)
    implementation(libs.navigation.compose)
    implementation(libs.lottie.compose)
    implementation(libs.toolkit.theme)
    implementation(libs.toolkit.foundation)
    implementation(libs.androidx.constraint.layout.compose)
    // Play
    implementation(libs.play.app.update.ktx)
    implementation(libs.play.app.review.ktx)
    implementation(libs.play.feature.delivery.ktx)
    // This is here temporarily needs to be moved to other pkg.
    implementation(libs.mp3agic)

    // bundles
    implementation(libs.bundles.icons)
    implementation(libs.bundles.compose.ui)
    debugImplementation(libs.bundles.compose.ui.tooling)
    api(project(":core"))
    implementation(project(":feature:widget"))
    implementation(libs.androidx.graphics.shapes)
}